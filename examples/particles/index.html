<html>

  <head>
    <title>Particle demo</title>
    <meta http-equiv="content-type" content="text/html; charset=ISO-8859-1">

    <link rel="stylesheet" href="https://maxcdn.bootstrapcdn.com/bootstrap/3.3.6/css/bootstrap.min.css" integrity="sha384-1q8mTJOASx8j1Au+a5WDVnPi2lkFfwwEAa8hDDdjZlpLegxhjVME1fgjWPGmkzs7" crossorigin="anonymous" />
    <link href="../examples.css" type="text/css" rel="stylesheet" media="screen" />
    <style>
      body {
        margin: 0px;
        background: #000;
      }
      #render-canvas {
        width: 100%;
        height: 100%;
      }
    </style>

    <script src="http://d1n0x3qji82z53.cloudfront.net/src-min-noconflict/ace.js" type="text/javascript" charset="utf-8"></script>
    <script type="text/javascript" src="../../dist/LumaGL.js"></script>
    <script type="text/javascript" src="../examples.js"></script>
    <script type="text/javascript" src="index.js"></script>
    <script id="scene-vs" type="x-shader/x-vertex">

      precision highp float;

      attribute vec3 aPosition;
      attribute vec2 aUV;
      attribute float aIndex;

      uniform mat4 uView;
      uniform mat4 uProjection;
      uniform sampler2D uPosition;
      uniform float uDataSize;
      uniform bool uReflect;

      varying vec2 uv;

      void main(void) {
        mat4 v = uView;
        mat3 iview = mat3(
          v[0][0], v[1][0], v[2][0],
          v[0][1], v[1][1], v[2][1],
          v[0][2], v[1][2], v[2][2]
        );
        float y = floor(aIndex/uDataSize);
        float x = aIndex - (y * uDataSize);
        x = x / uDataSize;
        y = y / uDataSize;
        vec3 o = texture2D(uPosition, vec2(x,y)).rgb;
        if (uReflect) {
          o.y = -o.y - 400.0;
        }
        gl_Position = uProjection * uView * vec4(iview * aPosition * 2.0 + o, 1.0);
        uv = aUV;
      }

    </script>



    <script id="scene-fs" type="x-shader/x-fragment">

      precision highp float;

      uniform sampler2D uSprite;
      uniform bool uReflect;
      uniform vec3 uColor;

      varying vec2 uv;

      void main(void) {
        float a = texture2D(uSprite, uv).a;
        vec3 q = uColor;
        if (uReflect) {
          q = mix(q, vec3(0.05, 0.05, 0.05), 0.5);
        }
        gl_FragColor = vec4(q,a);
      }

    </script>



    <script id="plane-vs" type="x-shader/x-vertex">

      attribute vec3 aPosition;
      attribute vec2 aUV;

      uniform mat4 uView;
      uniform mat4 uProjection;
      uniform mat4 uModel;

      varying vec2 vUV;
      varying vec2 vPos;

      void main(void) {
        gl_Position = uProjection * uView * uModel * vec4(aPosition, 1);
        vPos = (uModel * vec4(aPosition, 1)).xz;
        vUV = aUV;
      }

    </script>



    <script id="plane-fs" type="x-shader/x-vertex">

      precision highp float;

      uniform sampler2D uTexture;

      varying vec2 vUV;
      varying vec2 vPos;

      void main(void) {
        vec3 c = texture2D(uTexture, vUV * 4.0).rgb;
        float q = min(pow(400.0/length(vPos), 12.0), 1.0);
        gl_FragColor = vec4(c*q,1);
      }

    </script>



    <script id="quad-vs" type="x-shader/x-vertex">

      attribute vec2 aPosition;

      varying vec2 vPosition;

      void main(void) {
        gl_Position = vec4(aPosition, 0, 1);
        vPosition = aPosition;
      }

    </script>



    <script id="copy-fs" type="x-shader/x-fragment">

      precision highp float;

      uniform sampler2D uTexture;

      varying vec2 vPosition;

      void main(void) {
        vec2 p = 0.5 * vPosition + 0.5;
        gl_FragColor = vec4(texture2D(uTexture, p).rgb, 1.0);
      }

    </script>



    <script id="integrate-fs" type="x-shader/x-fragment">

      precision highp float;

      uniform sampler2D uPosition;
      uniform sampler2D uVelocity;

      varying vec2 vPosition;

      void main(void) {
        vec2 p = 0.5 * vPosition + 0.5;
        vec3 position = texture2D(uPosition, p).rgb;
        vec3 velocity = texture2D(uVelocity, p).rgb * 0.1;
        gl_FragColor = vec4(position + velocity, 1);
      }

    </script>



    <script id="accelerate-fs" type="x-shader/x-fragment">

      precision highp float;

      uniform sampler2D uVelocity;
      uniform sampler2D uPosition;
      uniform float uTime;
      uniform float uSpeed;

      varying vec2 vPosition;

      float snoise(vec4 v);

      float sample(vec3 v, float t) {
        return snoise(vec4(v, t) * 0.02);
      }

      vec3 displace(vec3 v) {
        float x = sample(v + 11.0, uTime);
        float y = sample(v + 37.0, uTime);
        float z = sample(v + 79.0, uTime);
        return mix(vec3(x,y,z), -v/128.0, 0.125);
      }

      void main(void) {
        vec2 p = 0.5 * vPosition + 0.5;
        vec3 velocity = texture2D(uVelocity, p).xyz;
        vec3 position = texture2D(uPosition, p).xyz;
        vec3 new = velocity * 0.8 + normalize(displace(position)) * uSpeed;
        gl_FragColor = vec4(new, 1);
      }

      //
      // Description : Array and textureless GLSL 2D/3D/4D simplex
      //               noise functions.
      //      Author : Ian McEwan, Ashima Arts.
      //  Maintainer : ijm
      //     Lastmod : 20110822 (ijm)
      //     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
      //               Distributed under the MIT License. See LICENSE file.
      //               https://github.com/ashima/webgl-noise
      //
      vec4 mod289(vec4 x) { return x - floor(x * (1.0 / 289.0)) * 289.0; }
      float mod289(float x) { return x - floor(x * (1.0 / 289.0)) * 289.0; }
      vec4 permute(vec4 x) { return mod289(((x*34.0)+1.0)*x); }
      float permute(float x) { return mod289(((x*34.0)+1.0)*x); }
      vec4 taylorInvSqrt(vec4 r) { return 1.79284291400159 - 0.85373472095314 * r; }
      float taylorInvSqrt(float r) { return 1.79284291400159 - 0.85373472095314 * r; }
      vec4 grad4(float j, vec4 ip) {
        const vec4 ones = vec4(1.0, 1.0, 1.0, -1.0);
        vec4 p,s;
        p.xyz = floor( fract (vec3(j) * ip.xyz) * 7.0) * ip.z - 1.0;
        p.w = 1.5 - dot(abs(p.xyz), ones.xyz);
        s = vec4(lessThan(p, vec4(0.0)));
        p.xyz = p.xyz + (s.xyz*2.0 - 1.0) * s.www;
        return p;
      }
      #define F4 0.309016994374947451
      float snoise(vec4 v) {
        const vec4  C = vec4( 0.138196601125011,
                              0.276393202250021,
                              0.414589803375032,
                             -0.447213595499958);
        vec4 i  = floor(v + dot(v, vec4(F4)) );
        vec4 x0 = v -   i + dot(i, C.xxxx);
        vec4 i0;
        vec3 isX = step( x0.yzw, x0.xxx );
        vec3 isYZ = step( x0.zww, x0.yyz );
        i0.x = isX.x + isX.y + isX.z;
        i0.yzw = 1.0 - isX;
        i0.y += isYZ.x + isYZ.y;
        i0.zw += 1.0 - isYZ.xy;
        i0.z += isYZ.z;
        i0.w += 1.0 - isYZ.z;
        vec4 i3 = clamp( i0, 0.0, 1.0 );
        vec4 i2 = clamp( i0-1.0, 0.0, 1.0 );
        vec4 i1 = clamp( i0-2.0, 0.0, 1.0 );
        vec4 x1 = x0 - i1 + C.xxxx;
        vec4 x2 = x0 - i2 + C.yyyy;
        vec4 x3 = x0 - i3 + C.zzzz;
        vec4 x4 = x0 + C.wwww;
        i = mod289(i);
        float j0 = permute( permute( permute( permute(i.w) + i.z) + i.y) + i.x);
        vec4 j1 = permute( permute( permute( permute (
                   i.w + vec4(i1.w, i2.w, i3.w, 1.0 ))
                 + i.z + vec4(i1.z, i2.z, i3.z, 1.0 ))
                 + i.y + vec4(i1.y, i2.y, i3.y, 1.0 ))
                 + i.x + vec4(i1.x, i2.x, i3.x, 1.0 ));
        vec4 ip = vec4(1.0/294.0, 1.0/49.0, 1.0/7.0, 0.0) ;
        vec4 p0 = grad4(j0,   ip);
        vec4 p1 = grad4(j1.x, ip);
        vec4 p2 = grad4(j1.y, ip);
        vec4 p3 = grad4(j1.z, ip);
        vec4 p4 = grad4(j1.w, ip);
        vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
        p0 *= norm.x;
        p1 *= norm.y;
        p2 *= norm.z;
        p3 *= norm.w;
        p4 *= taylorInvSqrt(dot(p4,p4));
        vec3 m0 = max(0.6 - vec3(dot(x0,x0), dot(x1,x1), dot(x2,x2)), 0.0);
        vec2 m1 = max(0.6 - vec2(dot(x3,x3), dot(x4,x4)            ), 0.0);
        m0 = m0 * m0;
        m1 = m1 * m1;
        return 49.0 * ( dot(m0*m0, vec3( dot( p0, x0 ), dot( p1, x1 ), dot( p2, x2 )))
                      + dot(m1*m1, vec2( dot( p3, x3 ), dot( p4, x4 ) ) ) ) ;
      }

    </script>






  </head>

  <body>
    <canvas id="render-canvas"></canvas>
  </body>

</html>
